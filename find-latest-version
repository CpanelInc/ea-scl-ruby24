#!/usr/local/cpanel/3rdparty/bin/perl
# cpanel - find-latest-version                  Copyright(c) 2019 cPanel, L.L.C.
#                                                           All rights Reserved.
# copyright@cpanel.net                                         http://cpanel.net
# This code is subject to the cPanel license. Unauthorized copying is prohibited

package ea_ruby::find_latest_version;

use strict;
use warnings;

use HTML::TreeBuilder::XPath ();

use FindBin;
use lib "../ea-tools/lib/ea4_tool";    # assumes ea-tools is checked out next to this repo
use ea4_tool::util ();

my $version;
my $name;
my $url;
my $type;
my $hex;
my $sha_page_fragment;

ea4_tool::util::find_latest_version( \&_get_required, \&_add_sum ) if !caller();

###############
#### helpers ##
###############

sub _get_required {
    my ($http) = @_;

    my $res = $http->get("https://www.ruby-lang.org/en/downloads/releases/");
    if ( !$res->{success} ) {
        die "Could not GET ruby info from https://www.ruby-lang.org/en/downloads/releases/\n";
    }

    my $dom      = HTML::TreeBuilder::XPath->new_from_content( $res->{content} );
    my $node_set = $dom->findnodes('//a');
    my @nodes    = $node_set->get_nodelist();

    foreach my $node (@nodes) {
        my $href = $node->attr('href');
        if ( !defined $version && $href && index( $href, 'https://cache.ruby-lang.org/pub/ruby/2.4' ) == 0 ) {
            my $idx        = rindex( $href, '/' );
            my $gz_tarball = substr( $href, $idx + 1 );

            # ruby-2.4.9.tar.gz
            if ( $gz_tarball =~ m/^ruby-(2\.4\.\d+)\.tar\.gz$/ ) {
                $version           = $1;
                $name              = "ruby-${version}.tar.gz";
                $url               = "https://cache.ruby-lang.org/pub/ruby/2.4/$name";
                $type              = 'sha256';
                $sha_page_fragment = "ruby-${version}-released";
                $sha_page_fragment =~ s/\./-/g;
            }
        }
        if ( defined $sha_page_fragment && !defined $hex ) {
            if ( $href && index( $href, $sha_page_fragment ) > 0 ) {
                $hex = _parse_sha_page( $http, "https://www.ruby-lang.org$href" );
            }
        }
    }

    return ( $version, $url, $name );
}

sub _parse_sha_page {
    my ( $http, $sha_page_url ) = @_;

    my $res = $http->get($sha_page_url);
    if ( !$res->{success} ) {
        die "Could not GET ruby info from $sha_page_url\n";
    }

    my $dom      = HTML::TreeBuilder::XPath->new_from_content( $res->{content} );
    my $node_set = $dom->findnodes('//a');
    my @nodes    = $node_set->get_nodelist();

    foreach my $node (@nodes) {
        my $href = $node->attr('href');
        if ( $href && index( $href, $name ) > 0 ) {

            my $grand_parent = $node->parent()->parent();
            my $code_node    = ( $grand_parent->find_by_tag_name('code') )[0];
            my $sha_line     = ( $code_node->content_list() )[0];

            if ( $sha_line =~ m/SHA256: ([^\s]+)/ ) {
                return $1;
            }
        }
    }

    return;
}

sub _add_sum {
    my ( $http, $hr ) = @_;

    if ($hex) {
        $hr->{tarball}{sum}{hex}  = $hex;
        $hr->{tarball}{sum}{type} = "sha256";
    }
    else {
        die "There is no SHA for $hr->{tarball}{name} (not officially released yet?)\n";
    }

    return;
}
